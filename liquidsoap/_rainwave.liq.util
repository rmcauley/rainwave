%include "_crossfade.liq.util"
%include "_relay_creds.liq.util"

def r4_backend() =
  result = process.read("cd /opt/rainwave && /home/rainwave/.local/bin/uv run rw_get_next.py --sid " ^ r4_sid)
  request.create(result)
end

r4_auto = request.dynamic(prefetch=0, timeout=2.0, r4_backend)
r4_auto.fetch()

def queue(remaining, _) =
  log("Queueing with #{remaining} seconds remaining")
  if not r4_auto.fetch() then
    log("Fetching next song failed")
  end
end

r4_auto = source.on_end(delay=5., r4_auto, queue)

r4_auto = amplify(0.7, override="replay_gain", stereo(r4_auto))
r4_auto = crossfade(r4_auto)

r4_offline = single("/home/icecast/~misc/offline.mp3")
r4_auto = fallback([r4_auto, r4_offline], track_sensitive=false)

ogg_mount = r4_dest_mount ^ ".ogg"
mp3_mount = r4_dest_mount ^ ".mp3"
relay_genre = "Video Game Music"

output.icecast(
  %opus(bitrate=112, signal="music"),
  host=relay_host,
  port=8000,
  password=relay_pass,
  mount=ogg_mount,
  genre=relay_genre,
  url=r4_dest_url,
  description=r4_dest_desc,
  source.drop.metadata(r4_auto)
)

output.icecast(
  %mp3.vbr(quality=7),
  host=relay_host,
  port=8000,
  password=relay_pass,
  mount=mp3_mount,
  genre=relay_genre,
  url=r4_dest_url,
  description=r4_dest_desc,
  r4_auto
)
